eJSLModel "Conference" {
    eJSL part: CMS Extension {
        entities {
            Entity Participant {
                attributes {
                    Attribute name {
                        type = Short_Text
                    }
                    Attribute affiliation {
                        type = Text
                    }
                    Attribute nationality {
                        type = Text
                    }
                    Attribute address {
                        type = Text
                    }
                }
            }
            Entity Talk {
                attributes {
                    Attribute title {
                        type = Short_Text
                    }
                    Attribute ^description {
                        type = Text
                    }
                    Attribute speaker {
                        type = Short_Text
                    }
                }
                references {
                    Reference {
 						entityAttribute = speaker
	                    referencedEntity = Participant
	                    referencedEntityAttribute = Participant.name
	                    max = 1
                    }
                } 
            }
            Entity Room {
                attributes {
                    Attribute roomname {
                        type = Short_Text
                    }
                    Attribute ^position {
                        type = Text
                    }
                }
            }
            Entity Programme {
                attributes {
                    Attribute title {
                        type = Short_Text
                    }
                    Attribute time {
                        type = Datetime
                    }
                    Attribute talk {
                        type = Short_Text
                    }
                    Attribute room {
                        type = Short_Text
                    }
                }
                references {
                    Reference {
                        entityAttribute = talk
                        referencedEntity = Talk
                        referencedEntityAttribute = Talk.title
                        max = 1
                    }
                    Reference {
                        entityAttribute = room
                        referencedEntity = Room
                        referencedEntityAttribute = Room.roomname
                        max = 1
                    }
                }
            }
        }
        pages {
            IndexPage Participants {
                *entities =  Participant
                representationColumns = Participant.name, Participant.address, Participant.affiliation
                filters = Participant.name, Participant.affiliation 
                links {
                    InternalContextLink Details {
                        target = Participant 
                        linkedAttribute = Participant.name
                        linkParameters {
                            Parameter name = *Attribute "Participant.name"
                        }
                    } 
                }
            }
            DetailsPage Participant {
                *entities =  Participant
                editFields {
                	Participant.name {
                		htmlType = Text_Field
                	},
                	Participant.address {
                		htmlType = Textarea
                	},
                	Participant.affiliation {
                		htmlType = Editor
                	},
                	Participant.nationality {
                		htmlType = Select
                		values {
                			en= "English",
                			de="German",
                			other="Other"
                		}                		
                	}
                }
                links {
                    InternalLink Index {
                        target = Participants 
                        linkedAttribute = name
                    }
                }
            }
            IndexPage Talks {
                *entities =  Talk
                representationColumns = Talk.title, Talk.speaker
                filters = Talk.title, Talk.speaker 
                links {
                    InternalContextLink Details {
                        target = Talk 
                        linkedAttribute = Talk.title
                        linkParameters {
                            Parameter title = *Attribute  "Talk.title"
                        }
                    }
                }
            }
            DetailsPage Talk {
                *entities =  Talk 
                links {
                    InternalLink Index {
                        target = Talks 
                        linkedAttribute = title
                    }
                }
            }
            IndexPage Rooms {
                *entities =  Room
                representationColumns = Room.roomname, Room.^position
                filters = Room.roomname, Room.^position 
                links {
                    InternalContextLink Details {
                        target = Room 
                        linkedAttribute = Room.roomname
                    }
                }
            }
            DetailsPage Room {
                *entities =  Room 
                links {
                    InternalLink Index {
                        target = Rooms 
                        linkedAttribute = roomname
                    }
                }
            }
            IndexPage Programme {
                *entities =  Programme
                representationColumns = Programme.room, Programme.talk, Programme.time, Programme.title
                filters = Programme.room, Programme.talk, Programme.time, Programme.title 
                links {
                    InternalContextLink Details {
                        target = Session 
                        linkedAttribute = Programme.title
                        linkParameters {
                            Parameter title = *Attribute  "Programme.title"
                        }
                    }
                }
            }
            DetailsPage Session {
                *entities = Programme
                links {
                    InternalLink Index {
                        target = Programme 
                        linkedAttribute = Programme.title
                    }
                }
            }
        }
        extensions {
            Component MyConference {
                Manifestation {
                    authors {
                        Author "John Doe" {
                            authorEmail = "john.doe@example.org"
                        }
                    }
                    copyright = "Copyright (C) 2019 All right reserved."
                    license = "GNU General Public License"
                    version = "1.0.0"
                }
                languages {
                    Language de-DE {
                        keyValuePairs {
                            COM_MYCONFERENCE_HELLO_WORLD = "Hallo Welt"
                        }
                    }
                    Language system de-DE {}
                    Language en-GB {}
                    Language system en-GB {}
                }
                sections {
                    FrontendSection {
                        *Pages {
                            *Page : Participants
                            *Page : Participant
                            *Page : Talks
                            *Page : Talk
                            *Page : Rooms
                            *Page : Room
                            *Page : Programme
                            *Page : Session
                        }
                    }
                    BackendSection {
                        *Pages {
                            *Page : Participants
                            *Page : Participant
                            *Page : Talks
                            *Page : Talk
                            *Page : Rooms
                            *Page : Room
                            *Page : Programme
                            *Page : Session
                        }
                    }
                }
            } 
            Module Talks {
            	Manifestation {
                    authors {
                        Author "John Doe" {
                            authorEmail = "john.doe@example.org"
                        }
                    }
                    copyright = "Copyright (C) 2019 All right reserved."
                    license = "GNU General Public License"
                    version = "1.0.0"
                }
                languages {
                    Language de-DE {}
                    Language system de-DE {}
                    Language en-GB {}
                    Language system en-GB {}
                }
                *Page : Talks from : MyConference data backendDAO
            }
        }   
    }
}